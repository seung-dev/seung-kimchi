plugins {
	id "java-library"
	id "maven-publish"
	id "signing"
}

def g = "io.github.seung-dev"
def n = "seung-kimchi"
def v = "1.0.0"

def ossrhUsername = project.findProperty("ossrhUsername")
def ossrhPassword = project.findProperty("ossrhPassword")
def gpgSecretKey = project.findProperty("gpgSecretKey")
def gpgPassphrase = project.findProperty("gpgPassphrase")

group = g
version = v

sourceCompatibility = JavaVersion.VERSION_17
targetCompatibility = JavaVersion.VERSION_17

repositories {
	mavenCentral()
}

dependencies {

	// test
	testImplementation("junit:junit:4.13")
	
	// lombok
	implementation("org.projectlombok:lombok:1.18.30")
	annotationProcessor("org.projectlombok:lombok:1.18.30")
	
	// jakarta
	implementation("jakarta.validation:jakarta.validation-api:2.0.2")
	implementation("jakarta.annotation:jakarta.annotation-api:1.3.5")
	
	// apache commons
	api("commons-io:commons-io:2.17.0")
	api("org.apache.commons:commons-lang3:3.14.0")
	api("commons-codec:commons-codec:1.17.0")
	api("org.apache.commons:commons-text:1.12.0")
	api("org.apache.commons:commons-compress:1.26.0")
	
	// json
	api("com.fasterxml.jackson.core:jackson-databind:2.17.1")
	
	// http
	api("com.konghq:unirest-java:3.14.5")
	
	// bouncycastle
	api("org.bouncycastle:bcprov-jdk18on:1.78.1")
	api("org.bouncycastle:bcpkix-jdk18on:1.78.1")
	
	// jwt
	implementation("io.jsonwebtoken:jjwt-api:0.12.6")
	runtimeOnly("io.jsonwebtoken:jjwt-impl:0.12.6")
	runtimeOnly("io.jsonwebtoken:jjwt-jackson:0.12.6")
	
	// tika
	api("org.apache.tika:tika-core:2.9.2")
	
	// unzip
	api("net.lingala.zip4j:zip4j:2.11.5")
	
	// poi
	api("org.apache.poi:poi:5.2.5")
	api("org.apache.poi:poi-ooxml:5.2.5")
	
	// pdf
	api("org.apache.pdfbox:pdfbox:3.0.2")
	
	// s3
	api("javax.xml.bind:jaxb-api:2.3.0")
	api("com.amazonaws:aws-java-sdk-s3:1.12.744")
	
	// calendar
	api("com.ibm.icu:icu4j:75.1")
	
}

java {
	withSourcesJar()
	withJavadocJar()
}

compileJava {
	options.encoding = "UTF-8"
}

javadoc {
	options.encoding = "UTF-8"
}

publishing {
	publications {
		mavenJava(MavenPublication) {
			from components.java
			artifact sourcesJar
			artifact javadocJar
			
			groupId = g
			artifactId = n
			version = v
			
			pom {
				name = "seung-kimchi"
				description = "Java Library"
				url = "https://github.com/seung-dev/seung-kimchi"
				licenses {
					license {
						name = "The Apache License, Version 2.0"
						url = "http://www.apache.org/licenses/LICENSE-2.0.txt"
					}
				}
				developers {
					developer {
						id = "seung"
						name = "Park Jong Seung"
						email = "seung.dev@gmail.com"
					}
				}
				scm {
					connection = "scm:git:git://github.com/seung-dev/seung-kimchi.git"
					developerConnection = "scm:git:ssh://github.com/seung-dev/seung-kimchi.git"
					url = "https://github.com/seung-dev/seung-kimchi"
				}
			}
		}
	}
	repositories {
		maven {
			name = "OSSRH"
			url = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
			credentials {
				username = ossrhUsername
				password = ossrhPassword
			}
		}
	}
}

signing {
	useInMemoryPgpKeys(gpgSecretKey, gpgPassphrase)
	sign publishing.publications.mavenJava
}
