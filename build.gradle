import com.vanniktech.maven.publish.SonatypeHost

plugins {
	id "java-library"
	id "com.vanniktech.maven.publish" version "0.30.0"
	id "signing"
}

def charset = "UTF-8"

group = "io.github.seung-dev"
archivesBaseName = project.name
version = "1.0.0"
description = "seung Library"

sourceCompatibility = "17"
targetCompatibility = "17"

repositories {
	mavenCentral()
}

dependencies {

	// test
	testImplementation("junit:junit:4.13")
	
	// lombok
	compileOnly group: "org.projectlombok", name: "lombok", version: "1.18.34"
	annotationProcessor group: "org.projectlombok", name: "lombok", version: "1.18.34"
	
	// jakarta
	implementation group: "jakarta.validation", name: "jakarta.validation-api", version: "3.1.0"
	implementation group: "jakarta.annotation", name: "jakarta.annotation-api", version: "3.0.0"
	
	// apache commons
	api group: "org.apache.commons", name: "commons-lang3", version: "3.17.0"
	api group: "org.apache.commons", name: "commons-text", version: "1.12.0"
	api group: "commons-io", name: "commons-io", version: "2.17.0"
	api group: "commons-codec", name: "commons-codec", version: "1.17.1"
	api group: "org.apache.commons", name: "commons-compress", version: "1.27.1"
	
	// json
	api group: "com.fasterxml.jackson.core", name: "jackson-databind", version: "2.18.0"
	
	// http
	api group: "com.konghq", name: "unirest-java", version: "3.14.5"
	
	// bouncycastle
	api group: "org.bouncycastle", name: "bcprov-jdk18on", version: "1.78.1"
	api group: "org.bouncycastle", name: "bcpkix-jdk18on", version: "1.78.1"
	
	// jwt
	implementation group: "io.jsonwebtoken", name: "jjwt-api", version: "0.12.6"
	runtimeOnly group: "io.jsonwebtoken", name: "jjwt-impl", version: "0.12.6"
	runtimeOnly group: "io.jsonwebtoken", name: "jjwt-jackson", version: "0.12.6"
	
	// tika
	api group: "org.apache.tika", name: "tika-core", version: "3.0.0"
	
	// unzip
	api group: "net.lingala.zip4j", name: "zip4j", version: "2.11.5"
	
	// poi
	api group: "org.apache.poi", name: "poi", version: "5.3.0"
	api group: "org.apache.poi", name: "poi-ooxml", version: "5.3.0"
	
	// pdf
	api group: "org.apache.pdfbox", name: "pdfbox", version: "3.0.3"
	
	// s3
	api group: "javax.xml.bind", name: "jaxb-api", version: "2.3.1"
	api group: "com.amazonaws", name: "aws-java-sdk-s3", version: "1.12.777"
	
	// calendar
	implementation group: "com.ibm.icu", name: "icu4j", version: "76.1"
	
}

compileJava {
	options.encoding = charset
}

javadoc {
	options {
		encoding = charset
		stylesheetFile = file("styles/javadoc.css")
		links("https://javadoc.io/doc/com.fasterxml.jackson.core/jackson-databind/latest/index.html")
	}
}

tasks.build {
	dependsOn tasks.javadoc
}

task javadocJar(type: Jar) {
	archiveClassifier.set("javadoc")
	from javadoc
}

task sourcesJar(type: Jar) {
	archiveClassifier.set("sources")
	from sourceSets.main.allSource
}

mavenPublishing {
	publishToMavenCentral(SonatypeHost.CENTRAL_PORTAL)
	signAllPublications()
	coordinates(group, archivesBaseName, version)
	pom {
		name = archivesBaseName
		description = project.description
		url = "https://github.com/seung-dev/" + archivesBaseName
		licenses {
			license {
				name = "The Apache License, Version 2.0"
				url = "http://www.apache.org/licenses/LICENSE-2.0.txt"
			}
		}
		developers {
			developer {
				id = "seung"
				name = "Park Jong Seung"
				email = "seung.dev@gmail.com"
			}
		}
		scm {
			connection = "scm:git:git://github.com/seung-dev/seung-kimchi.git"
			developerConnection = "scm:git:ssh://github.com/seung-dev/seung-kimchi.git"
			url = "https://github.com/seung-dev/seung-kimchi"
		}
	}
}
